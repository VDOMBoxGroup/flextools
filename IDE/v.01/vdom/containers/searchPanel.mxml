<?xml version="1.0" encoding="utf-8"?>
<ActionPanel 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	width="250"
	xmlns="vdom.containers.*">
	
	<mx:Metadata>
	[ResourceBundle("ApplicationManagment")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import mx.collections.SortField;
			import mx.collections.Sort;
			import vdom.events.SearchPanelEvent;
			import mx.utils.StringUtil;
			import mx.collections.XMLListCollection;

			[ Bindable ]
			private var _listApplication : XMLListCollection;

			public function set listApplication( value : XMLList ) : void
			{
				if ( value == null || value.length() == 0 )
				{
					setEnable( false );
					_listApplication = null;
					return ;
				}
				setEnable( true );
				
				var list : XMLList = new XMLList( value );
				list = <Application ID="0" Name="-All Applications-" /> + list;
				var newCollection : XMLListCollection = new XMLListCollection( list );
				
				var sort : Sort = new Sort();
				sort.fields = [ new SortField( "@Name" ) ];
				newCollection.sort = sort;
				newCollection.refresh();
				
				_listApplication = newCollection;
				applicationList.selectedIndex = 0;
			}

			public function set applicationId( value : String ) : void
			{
				if ( !value )
					return ;
				applicationList.selectedItem = applicationList.dataProvider.source.( @ID == value )[ 0 ];
			}
			
			private function setEnable( flag : Boolean ) : void
			{
				try
				{
					searchField.enabled = flag;
					applicationList.enabled = flag;
					searchButton.enabled = flag;
				}
				catch( error : Error ) {}
			}
			
			private function search() : void
			{
				var searchString : String = StringUtil.trim( searchField.text );

				if ( !searchString || !applicationList.selectedItem || !applicationList.selectedItem.@ID[ 0 ] )
					return ;
				
				var applicationID : String = applicationList.selectedItem.@ID[ 0 ];
				applicationID = applicationID == "0" ? "" : applicationID;
				
				var spe : SearchPanelEvent = new SearchPanelEvent( SearchPanelEvent.SEARCH_PARAM_CHANGED );

				spe.applicationId = applicationID;
				spe.searchString = searchString;

				dispatchEvent( spe );
			}
		]]>
	</mx:Script>
	<mx:HBox width="100%">
		<mx:VBox width="100%"
			horizontalAlign="left" verticalGap="4">
			<mx:TextInput 
				id="searchField"
				width="160"/>
			<mx:ComboBox 
				id="applicationList"
				dataProvider="{_listApplication}" labelField="@Name" width="160"
				cornerRadius="0"/>
		</mx:VBox>
		<mx:Button
			id="searchButton"
			height="50" label="{resourceManager.getString('ApplicationManagment','search_find')}"
			cornerRadius="0"
			click="search()"/>
	</mx:HBox>
</ActionPanel>

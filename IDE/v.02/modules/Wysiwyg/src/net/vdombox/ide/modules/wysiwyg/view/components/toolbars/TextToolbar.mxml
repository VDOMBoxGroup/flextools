<?xml version="1.0" encoding="utf-8"?>
<s:HGroup xmlns:controls="net.vdombox.ide.modules.wysiwyg.view.components.controls.*"
		  xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  enabled="false"
		  verticalAlign="middle"
		  implements="net.vdombox.ide.modules.wysiwyg.interfaces.IToolbar">

	<fx:Script>
		<![CDATA[
			import com.zavoo.svg.nodes.SVGNode;
			
			import mx.collections.ArrayCollection;
			import mx.collections.IViewCursor;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.core.UIComponent;
			import mx.events.ColorPickerEvent;
			import mx.managers.FocusManager;
			import mx.managers.IFocusManagerComponent;
			import mx.utils.StringUtil;
			
			import net.vdombox.ide.common.model._vo.AttributeVO;
			import net.vdombox.ide.common.model._vo.VdomObjectAttributesVO;
			import net.vdombox.ide.modules.wysiwyg.interfaces.IRenderer;
			
			import spark.components.RichEditableText;

			private var fonts : ArrayCollection = new ArrayCollection( [ { label: "Andale Mono", data: "andale" },
				{ label: "Arial", data: "arial" },
				{ label: "Arial Black", data: "arial" },
				{ label: "Book Antiqua", data: "book" },
				{ label: "Comic Sans MS", data: "comic" },
				{ label: "Courier New", data: "courier" },
				{ label: "Georgia", data: "georgia" },
				{ label: "Helvetica", data: "helvetica" },
				{ label: "Impact", data: "impact" },
				{ label: "Symbol", data: "symbol" },
				{ label: "Tahoma", data: "tahoma" },
				{ label: "Terminal", data: "terminal" },
				{ label: "Times New Roman",
					data: "times" },
				{ label: "Times", data: "times" },
				{ label: "Verdana", data: "verdana" },
				{ label: "Webdings", data: "webdings" },
				{ label: "Wingdings", data: "wingdings" } ] );

			private var fontSize : ArrayCollection = new ArrayCollection( [ { label: "8 px", data: "8" },
				{ label: "10 px", data: "10" },
				{ label: "12 px", data: "12" },
				{ label: "14 px", data: "14" },
				{ label: "18 px", data: "18" },
				{ label: "24 px", data: "24" },
				{ label: "36 px", data: "36" }, ] );


			private var attributesObject : Object;

			private var _item : IRenderer;

			private var isInited : Boolean;
			
			private var attributeName : String;

			public function get elementForEditing():Object
			{
				return item.editableComponent;
			}

			public function get attributes() : Array
			{
				var result : Array = [];
				var attributeVO : AttributeVO;

				if ( attributesObject )
				{
					for each ( attributeVO in attributesObject )
					{
						result.push( attributeVO );
					}
				}

				return result;
			}

			public function get item() : IRenderer
			{
				return _item;
			}

			private function onKeyDown(evt:KeyboardEvent):void
			{
				switch (evt.keyCode)
				{
					case Keyboard.DELETE :
						evt.stopImmediatePropagation();
						break;
				}
			}
			
			private function onFocusOut( event : FocusEvent ) : void
			{
				attributeName = "value";
				
				if ( attributesObject[ attributeName ].value == elementForEditing.text )
					return;
				
				dispatchEvent( new Event( Event.CHANGE ) );
				
				dispatchEvent( event );
			}
			
			public function init( item : IRenderer ) : void
			{
				try
				{
				_item = item;
					
				elementForEditing.setStyle( "paddingTop", 5 );
				elementForEditing.setStyle( "paddingBottom", 5 );
				elementForEditing.setStyle( "paddingLeft", 5 );
				elementForEditing.setStyle( "paddingRight", 5 );

				attributesObject = extractAttributes( _item );

				if ( !attributesObject )
					return;

				applyDefaultStates();


				
				elementForEditing.editable = true;
				elementForEditing.selectable = true;

				if (elementForEditing is RichEditableText)
				{
					RichEditableText(elementForEditing).addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown, true);
					//RichEditableText(item.editableComponent).addEventListener(FocusEvent.FOCUS_OUT, onFocusOut );
				}

				RichEditableText( elementForEditing ).setFocus();
				focusManager.setFocus( elementForEditing as IFocusManagerComponent );
				
				visible = false;
				includeInLayout = false;

				isInited = true;
				enabled = true;

				try
				{
					Object( _item ).scroller.visible = false;
				}
				catch ( error : Error )
				{
				}
				}
				catch ( error : Error )
				{
					var t : int = 9;
				}
			}

			public function refresh( item : IRenderer ) : void
			{
				
			}
			
			public function close() : void
			{
				if ( !isInited )
					return;

				enabled = false;
				isInited = false;
				
				attributeName = "value";
				if ( attributesObject[ attributeName ].value != elementForEditing.text )
				{
					attributesObject[ "value" ].value = elementForEditing.text;
				
					
				
					dispatchEvent( new Event( Event.CHANGE ) );
				}

				var attributeValue : String;

				elementForEditing.setStyle( "paddingTop", 0 );
				elementForEditing.setStyle( "paddingBottom", 0 );
				elementForEditing.setStyle( "paddingLeft", 0 );
				elementForEditing.setStyle( "paddingRight", 0 );
				elementForEditing.editable = false;
				elementForEditing.selectable = false;

				UIComponent( item ).setFocus();
			}
			
			public function saveValue() : String
			{
				return "";
			}

			private function extractAttributes( item : IRenderer ) : Object
			{
				var editableAttributeVO : AttributeVO;
				var attributeVO : AttributeVO;

				for each ( attributeVO in item.renderVO.attributes )
				{
					if ( attributeVO.name == "editable" )
					{
						editableAttributeVO = attributeVO;
						break;
					}
				}

				if ( !editableAttributeVO )
					return null;

				var attributesObject : Object = {};
				var attributeName : String;
				var attributeNames : Array = editableAttributeVO.value.split( "," );

				for each ( attributeName in attributeNames )
				{
					attributeName = StringUtil.trim( attributeName );
					attributesObject[ attributeName ] = null;
				}

				var editableContent : XML = item.renderVO.content.( "@editable" )[ 0 ];
				var editableContentAttributes : XMLList = editableContent.attributes();
				var editableContentAttribute : XML;

				for each ( editableContentAttribute in editableContentAttributes )
				{
					attributeName = editableContentAttribute.name().toString();

					if ( attributesObject.hasOwnProperty( attributeName ) )
						attributesObject[ attributeName ] = new AttributeVO( attributeName, editableContentAttribute[ 0 ] );

					// TODO Fix

					if ( attributeName == "textalign" )
						attributesObject[ "align" ] = new AttributeVO( "align", editableContentAttribute[ 0 ] );
				}

				if ( attributesObject.hasOwnProperty( "value" ) )
					attributesObject[ "value" ] = new AttributeVO( "value", editableContent[ 0 ] );

				return attributesObject;
			}

			private function applyDefaultStates() : void
			{
				fonts.sort = new Sort();
				fonts.sort.fields = [ new SortField( "data" ) ];
				fonts.refresh();

				fontSelector.dataProvider = fonts;
				fontSelector.validateNow();

				var cursor : IViewCursor = fonts.createCursor();

				if ( attributesObject.hasOwnProperty( "fontfamily" ) )
				{
					if ( cursor.findFirst( { data: attributesObject[ "fontfamily" ].value.toLowerCase() } ) )
						fontSelector.selectedItem = cursor.current;
				}


				fontSize.sort = new Sort();
				fontSize.sort.fields = [ new SortField( "data", false, false, true ) ];
				fontSize.refresh();

				sizeSelector.dataProvider = fontSize;
				sizeSelector.validateNow();

				cursor = fontSize.createCursor();

				if ( attributesObject.hasOwnProperty( "fontsize" ) )
				{
					if ( cursor.findFirst( { data: attributesObject[ "fontsize" ].value.toLowerCase() } ) )
						sizeSelector.selectedItem = cursor.current;
					else
						sizeSelector.selectedItem = null;
				}

				if ( attributesObject.hasOwnProperty( "fontweight" ) )
				{
					if ( attributesObject[ "fontweight" ].value == "bold" )
						weightButton.selected = true;
					else
						weightButton.selected = false;
				}

				if ( attributesObject.hasOwnProperty( "fontstyle" ) )
				{
					if ( attributesObject[ "fontstyle" ].value == "italic" )
						styleButton.selected = true;
					else
						styleButton.selected = false;
				}

				if ( attributesObject.hasOwnProperty( "textdecoration" ) )
				{
					if ( attributesObject[ "textdecoration" ].value == "underline" )
						decorationButton.selected = true;
					else
						decorationButton.selected = false;
				}

				if ( attributesObject.hasOwnProperty( "color" ) )
				{
					var colorValue : String = attributesObject[ "color" ].value;

					if ( colorValue.charAt() == "#" )
						colorValue = colorValue.substr( 1 );

					textColor.selectedColor = int( "0x" + colorValue );
				}

				if ( attributesObject.hasOwnProperty( "align" ) )
				{
					if ( attributesObject[ "align" ].value == "center" )
					{
						leftButton.selected = false;
						centerButton.selected = true;
						rightButton.selected = false;
					}

					else if ( attributesObject[ "align" ].value == "right" )
					{
						leftButton.selected = false;
						centerButton.selected = false;
						rightButton.selected = true;
					}
					else
					{
						leftButton.selected = true;
						centerButton.selected = false;
						rightButton.selected = false;
					}
				}
			}

			private function changeWeight() : void
			{
				if ( !attributesObject.hasOwnProperty( "fontweight" ) )
					return;

				if ( !weightButton.selected )
					attributesObject[ "fontweight" ].value = "normal";
				else
					attributesObject[ "fontweight" ].value = "bold";

				elementForEditing.setStyle( "fontWeight", attributesObject[ "fontweight" ].value );
				
				attributeName = "fontweight";
				
				dispatchEvent( new Event( Event.CHANGE ) );
			}

			private function changeStyle() : void
			{
				if ( !attributesObject.hasOwnProperty( "fontstyle" ) )
					return;

				if ( !styleButton.selected )
					attributesObject[ "fontstyle" ].value = "normal";
				else
					attributesObject[ "fontstyle" ].value = "italic";

				elementForEditing.setStyle( "fontStyle", attributesObject[ "fontstyle" ].value );
				
				attributeName = "fontstyle";
				
				dispatchEvent( new Event( Event.CHANGE ) );
			}

			private function changeDecoration() : void
			{
				if ( !attributesObject.hasOwnProperty( "textdecoration" ) )
					return;

				if ( !decorationButton.selected )
					attributesObject[ "textdecoration" ].value = "none";
				else
					attributesObject[ "textdecoration" ].value = "underline";

				elementForEditing.setStyle( "textDecoration", attributesObject[ "textdecoration" ].value );
				
				attributeName = "textdecoration";
				
				dispatchEvent( new Event( Event.CHANGE ) );
			}

			private function changeAlign( value : String ) : void
			{
				if ( !attributesObject.hasOwnProperty( "align" ) )
					return;

				switch ( value )
				{
					case "left":
					{
						attributesObject[ "align" ].value = "left";

						leftButton.selected = true;
						centerButton.selected = false;
						rightButton.selected = false;

						break;
					}
					case "center":
					{
						attributesObject[ "align" ].value = "center";

						leftButton.selected = false;
						centerButton.selected = true;
						rightButton.selected = false;

						break;
					}
					case "right":
					{
						attributesObject[ "align" ].value = "right";

						leftButton.selected = false;
						centerButton.selected = false;
						rightButton.selected = true;

						break;
					}
				}

				elementForEditing.setStyle( "textAlign", attributesObject[ "align" ].value );
				
				attributeName = "align";
				
				dispatchEvent( new Event( Event.CHANGE ) );
			}

			private function colorTextChanged( event : ColorPickerEvent ) : void
			{
				if ( !attributesObject.hasOwnProperty( "color" ) )
					return;

				var hexValue : String = event.color.toString( 16 );
				var hexLength : int = hexValue.length;

				for ( var i : int = hexLength; i < 6; i++ )
				{
					hexValue = "0" + hexValue;
				}

				attributesObject[ "color" ].value = hexValue;

				elementForEditing.setStyle( "color", event.color );
				
				attributeName = "color";
				
				dispatchEvent( new Event( Event.CHANGE ) );
			}

			private function changeFamily( value : Object ) : void
			{
				if ( !attributesObject.hasOwnProperty( "fontfamily" ) )
					return;

				if ( value && value.data )
				{
					attributesObject[ "fontfamily" ].value = value.data;
					elementForEditing.setStyle( "fontFamily", value.data );
					
					attributeName = "fontfamily";
					
					dispatchEvent( new Event( Event.CHANGE ) );
				}
			}

			private function changeSize( value : Object ) : void
			{
				if ( !attributesObject.hasOwnProperty( "fontsize" ) )
					return;

				if ( value && value.data )
				{
					attributesObject[ "fontsize" ].value = value.data;
					elementForEditing.setStyle( "fontSize", value.data );
					
					attributeName = "fontsize";
					
					dispatchEvent( new Event( Event.CHANGE ) );
				}
			}
			
			public function get vdomObjectAttributesVO() : VdomObjectAttributesVO
			{
				var vdomObjectAttributesVO : VdomObjectAttributesVO = new VdomObjectAttributesVO(_item.vdomObjectVO);
				var attributesVO : Array = [];
				
				var attributeVO : AttributeVO;
				if ( attributeName == "value" )
				{
					/*attributeVO = new AttributeVO( attributeName );*/
					attributesObject[ attributeName ].value = elementForEditing.text;
					attributeVO = attributesObject[ attributeName ].clone();
					/*attributeVO.value = attributesObject[ attributeName ].value;*/
					attributesVO.push( attributeVO );
				}
				else
				{
					//attributeVO = new AttributeVO( attributeName );
					attributeVO = attributesObject[ attributeName ].clone();
					attributesVO.push( attributeVO );
					
					//attributeVO = new AttributeVO( "value" );
					attributesObject[ "value" ].value = elementForEditing.text;
					attributeVO = attributesObject[ "value" ].clone();
					attributesVO.push( attributeVO );
				}
				
				vdomObjectAttributesVO.attributes = attributesVO;
				
				return vdomObjectAttributesVO;
			}
		]]>
	</fx:Script>

	<controls:ToolbarButton id="weightButton"
							focusEnabled="false" height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_bold.png')" styleName="toolbarButton" width="24"
							click="changeWeight()"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_bold' ) }"/>

	<controls:ToolbarButton id="styleButton"
							height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_italic.png')" styleName="toolbarButton" width="24"
							click="changeStyle()"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_italic' ) }"/>

	<controls:ToolbarButton id="decorationButton"
							focusEnabled="false" height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_underline.png')" styleName="toolbarButton" width="24"
							click="changeDecoration()"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_underline' ) }"/>

	<mx:VRule height="24"/>

	<controls:ToolbarButton id="leftButton"
							height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_left.png')" styleName="toolbarButton" width="24"
							click="changeAlign('left')"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_justify_left' ) }"/>

	<controls:ToolbarButton id="centerButton"
							height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_center.png')" styleName="toolbarButton" width="24"
							click="changeAlign('center')"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_justify_center' ) }"/>

	<controls:ToolbarButton id="rightButton"
							height="24" icon="@Embed(source='../assets/toolbars/richTextToolbar/icon_right.png')" styleName="toolbarButton" width="24"
							click="changeAlign('right')"
							toolTip="{ resourceManager.getString( 'Wysiwyg_General', 'richtext_toolbar_justify_right' ) }"/>

	<mx:VRule height="24"/>

	<s:Label text="{ resourceManager.getString( 'Wysiwyg_General', 'text_toolbar_font' ) }"/>

	<mx:ComboBox id="fontSelector"
				 focusEnabled="false" height="24" styleName="toolbarComboBox" width="100"
				 color="black"
				 change="changeFamily(fontSelector.selectedItem)"/>

	<mx:VRule height="24"/>

	<s:Label text="{ resourceManager.getString( 'Wysiwyg_General', 'text_toolbar_size' ) }"/>

	<mx:ComboBox id="sizeSelector"
				 height="24" styleName="toolbarComboBox" width="100"
				 color="black"
				 change="changeSize(sizeSelector.selectedItem)"/>

	<s:Label text="{ resourceManager.getString( 'Wysiwyg_General', 'text_toolbar_text_color' ) }"/>

	<mx:ColorPicker id="textColor"
					focusEnabled="false" height="24" width="36"
					change=" colorTextChanged(event)"/>
</s:HGroup>

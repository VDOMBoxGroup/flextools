<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" 
					  xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300"
					  implements="net.vdombox.ide.modules.dataBase.interfaces.IEditor" 
					  xmlns:components="net.vdombox.components.*" xmlns:components1="net.vdombox.ide.modules.dataBase.view.components.*">	
	
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.LinkButton;
			import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.AdvancedDataGridEvent;
			import mx.events.DataGridEvent;
			import mx.events.FlexEvent;
			import mx.utils.ObjectUtil;
			import mx.utils.StringUtil;
			import mx.utils.UIDUtil;
			
			import net.vdombox.ide.common.events.EditorEvent;
			import net.vdombox.ide.common.model._vo.ObjectVO;
			import net.vdombox.ide.modules.dataBase.events.QueueEvent;
			import net.vdombox.ide.modules.dataBase.model.vo.StructureVO;
			import net.vdombox.ide.modules.dataBase.view.components.ItemRenderers.BoldSearchItemRenderer;
			import net.vdombox.ide.modules.dataBase.view.components.ItemRenderers.TextInputItemRenderers;
			import net.vdombox.ide.modules.dataBase.view.components.Queue.QueueManager;
			
			private var _objectVO : ObjectVO;
			
			private var manager : *;
			private var queue : QueueManager;
			private var queryResult:XML;
			private var tableStructureXML:XML;
			public var dataGridColumns:Array;
			private var dataGridColumnsProps:Array = [];
			private var currentPage:int;
			private var pages:Array = []; /* of XML */
			private var externalValue:String;
			private var thereAreGlobalChanges:Boolean = false;
			private var verticalScrollPositionStore:int = 0;
			private var editableValue:String = '';
			
			[Bindable]
			private var dataGridProvider:ArrayCollection = new ArrayCollection();
			private var dataGridProviderOld:Dictionary = new Dictionary( true );
			
			private var AMOUNT:int = 500;
			
			[Bindable]
			private var totalRecords:int = 0;
			
			[Bindable]
			private var alertMessage:String = "";
			
			public function get editorID() : String
			{
				return _objectVO ? _objectVO.id : "";
			}
			
			public function get editorName() : String
			{
				return _objectVO ? _objectVO.name : "";
			}
			
			public function get objectVO() : Object
			{
				return _objectVO ? _objectVO : null;
			}
			
			public function set objectVO( value : Object ) : void
			{
				_objectVO = value as ObjectVO;
			}
			
			// Table
			
			
			public function set externalManager( ref: * ) : void 
			{
				manager = ref;
				
				onLoadInit();
			}
			
			public function set value( value : String ) : void 
			{
				externalValue = value;
			}
			
			public function get value() : String 
			{
				if ( thereAreGlobalChanges )
					return "modified";
				else
					return externalValue;
			}
			
			private function onLoadInit() : void 
			{
				queue = new QueueManager( manager );
				
				/* Permanent event listener */
				queue.addEventListener( QueueEvent.QUEUE_INTERRUPT, errorHandler );	
				
				queue.reset();
				//queue.addRequest( UIDUtil.createUID(), "get_structure", "", structureResponseHandler, null) ;
				queue.addRequest( UIDUtil.createUID(), 'get_structure', '', structureResponseHandler, null);
				queue.addRequest( UIDUtil.createUID(), "get_count", "", countResponseHandler, null );
				
				/* Request first page */
				currentPage = 0;
				queue.addRequest(
					UIDUtil.createUID(),
					'get_data',
					'<range><limit>' + AMOUNT.toString() + '</limit><offset>' + String(AMOUNT * currentPage) + '</offset></range>',
					getPageResponseHandler, null );
				
				queue.addEventListener( QueueEvent.QUEUE_COMPLETE, queueOnLoadCompleteHandler );
				
				updateQueueLength();
				
				queue.execute();
				
				currentState = "Progress";
			}
			
			private function queueOnLoadCompleteHandler( message : String ) : void 
			{
				queue.removeEventListener( QueueEvent.QUEUE_COMPLETE, queueOnLoadCompleteHandler );
				currentState = "Result";
				updateQueueLength();
			}
			
			private function errorHandler( event : QueueEvent ) : void 
			{
				showMessage( event.message );
				updateQueueLength();	
			}
			
			private function showMessage( message : String ) : void 
			{
				alertMessage = message;
				//__alertArea.selectedChild = __message;
			}
			
			private function updateQueueLength() : void 
			{
				/*if (queue)
				__queueLenght.text = queue.length.toString();*/
			}
			
			private function structureResponseHandler( message : String ) : void 
			{
				try 
				{
					queryResult = new XML( message );
					tableStructureXML = new XML( queryResult.Result.tablestructure.table.header );
					
					setTableHeaders();
				}
				catch (err:Error) 
				{
					/* error04 */
					showMessage( "External Manager error (04): Can not convert result into XML or result error!" );
					queryResult = new XML();
				}
			}
			
			
			private function fieldSortNumber(field : String):Function
			{
				return function sortNumber(obj1:Object, obj2:Object):int
				{
					var num:Number = ((Number)(obj1[field]) - (Number)(obj2[field]));
					return (num > 0) ? 1 : ((num < 0) ? -1 : 0);

				}
			}
			
			private function setTableHeaders():void 
			{
				if ( !dataGridColumns )
				{	
					dataGridColumns = [];
					for each (var xmlHeader:XML in tableStructureXML.column) 
					{
						var _header:AdvancedDataGridColumn = new AdvancedDataGridColumn();
						var columnProps : StructureVO = new StructureVO( xmlHeader );
					
						_header.itemRenderer = new ClassFactory( BoldSearchItemRenderer );
						dataGridColumnsProps.push( columnProps );
						_header.dataField = xmlHeader.@name;
						
						if ( xmlHeader.@type == "INTEGER" || xmlHeader.@type == "REAL" )
							_header.sortCompareFunction = fieldSortNumber( xmlHeader.@name );
				
						dataGridColumns.push( _header );
					
					}
					dataTable.columns = dataGridColumns;
				}
				
				
			}
			
			private function countResponseHandler( message : String ) : void 
			{
				try 
				{
					queryResult = new XML( message );
					totalRecords = int( queryResult.Result );		
				}
				catch ( err : Error ) 
				{ 
					return;	
				}
			}
			
			private function getPageResponseHandler( message : String ) : void 
			{
				try 
				{
					queryResult = new XML( message );
					/*tableStructureXML = new XML( queryResult.Result.queryresult.table.header );
					
					setTableHeaders();*/
					pages[currentPage] = new XML(queryResult.Result.queryresult.table.data );
					
					showPageData();
				}
				catch ( err : Error ) 
				{
					queryResult = new XML();
				}
				
				pagesArea.enabled = true;
			}
			
			private function showPageData() : void 
			{
				if ( !pages[ currentPage ] ) 
				{
					getPageRequest();
					return;		
				}
				
				/* Show current page */
				dataGridProvider = new ArrayCollection();
				dataGridProviderOld = new Dictionary( true );
				for each ( var xmlRow:XML in pages[ currentPage ].row ) 
				{
					/* Create tableRow object */
					var tableRow:Object = new Object();
					var cellIndex:int = 0;
					for each ( var xmlCell:XML in xmlRow.cell ) 
					{
						tableRow[ dataGridColumns[ cellIndex ].dataField ] = xmlCell.toString();
						cellIndex++;
					}
					
//					tableRow["fnew"] = false;
//					tableRow["changed"] = false;
					tableRow["GUID"] = UIDUtil.createUID(); 
					dataGridProvider.addItem( tableRow );
					dataGridProviderOld[ tableRow["id"] ] = ObjectUtil.copy( tableRow );
				}
				
				buildPagesTickets();

			}
			
			// FIXME: Большая запутанная функция.
			private function buildPagesTickets() : void 
			{
				/* (Re)Build pages tickets */
				amount.text = AMOUNT.toString();
				pagesArea.removeAllChildren();
				
				var lastPageNumber:int = Math.floor(totalRecords / AMOUNT) - 1;
				if ( totalRecords % AMOUNT > 0 )
					lastPageNumber++;  			
				
				var fp:int;
				var lp:int;
				
				fp = currentPage - 5;
				lp = currentPage + 5;
				
				if ( fp < 0 && lp <= lastPageNumber ) 
				{
					lp += Math.abs(fp);
					if ( lp > lastPageNumber ) 
						lp = lastPageNumber;
					fp = 0;
				}
				
				if ( fp >= 0 && lp > lastPageNumber ) 
				{
					fp -= lp - lastPageNumber;
					if (fp < 0)
						fp = 0;
					lp = lastPageNumber;
				}
				
				if ( fp < 0 && lp > lastPageNumber ) 
				{
					fp = 0;
					lp = lastPageNumber;
				}
				
				
				if ( fp != 0 ) 
				{
					var leftShift:LinkButton = new LinkButton();
					leftShift.label = "1";
					leftShift.height = 18;
					leftShift.setStyle( "paddingLeft", 1 );
					leftShift.setStyle( "paddingRight", 1 );
					leftShift.addEventListener( MouseEvent.CLICK, pageClickHandler );
					pagesArea.addChild( leftShift );
					
					pageItem = new LinkButton();
					pageItem.label = '...';
					pageItem.height = 18;
					pageItem.setStyle( "paddingLeft", 1 );
					pageItem.setStyle( "paddingRight", 1 );
					pageItem.enabled = false;
					pagesArea.addChild( pageItem );
				}
				
				var pageItem:LinkButton;
				
				for ( var p:int = fp; p <= lp; p++ )  
				{
					pageItem = new LinkButton();
					pageItem.label = ( p + 1 ).toString();
					pageItem.height = 18;
					pageItem.setStyle( "paddingLeft", 1 );
					pageItem.setStyle( "paddingRight", 1 );
					if ( p == currentPage )
						pageItem.setStyle( "textDecoration", "underline" );
					else
						pageItem.addEventListener( MouseEvent.CLICK, pageClickHandler );
					
					pagesArea.addChild( pageItem );
				}
				
				if ( lp != lastPageNumber ) 
				{
					pageItem = new LinkButton();
					pageItem.label = '...';
					pageItem.height = 18;
					pageItem.setStyle( "paddingLeft", 1 );
					pageItem.setStyle( "paddingRight", 1 );
					pageItem.enabled = false;
					pagesArea.addChild( pageItem );
					
					var rightShift:LinkButton = new LinkButton();
					rightShift.label = String( lastPageNumber + 1 );
					rightShift.height = 18;
					rightShift.setStyle( "paddingLeft", 1 );
					rightShift.setStyle( "paddingRight", 1 );
					rightShift.addEventListener( MouseEvent.CLICK, pageClickHandler );
					pagesArea.addChild( rightShift );
				}
				
				
			}
			
			private function pageClickHandler( mEvent : MouseEvent ) : void 
			{
				/* Handle page number click */
				currentPage = int( mEvent.currentTarget.label ) - 1; 
				showPageData();
			}
			
			private function getPageRequest():void {
				
				pagesArea.enabled = false;
				
				queue.reset();
				queue.addRequest(
					UIDUtil.createUID(),
					'get_data',
					'<range><limit>' + AMOUNT.toString() + '</limit><offset>' + String( AMOUNT * currentPage ) + '</offset></range>',
					getPageResponseHandler, null );
				
				updateQueueLength();
				
				queue.addEventListener( QueueEvent.QUEUE_COMPLETE, getPageQueueCompleteHandler );
				queue.execute();
				currentState = "Progress";
			}
			
			private function getPageQueueCompleteHandler( event : QueueEvent ) : void 
			{
				queue.removeEventListener( QueueEvent.QUEUE_COMPLETE, getPageQueueCompleteHandler );
				currentState = "Result";
				
				updateQueueLength();
			}
			
			protected function dataTable_keyDownHandler( event : KeyboardEvent ) : void
			{
				if ( event.keyCode == Keyboard.ENTER && dataTable.dataProvider.source[dataTable.dataProvider.length - 1] == dataTable.selectedItem )
				{
					addRowBtnClickHandler();
				}
			}
			
			private function addRowBtnClickHandler() : void 
			{
				/* Create tableRow object */
				var tableRow:Object = new Object();
				var columnIndex:int = 0;
				for each ( var dgColumn:AdvancedDataGridColumn in dataGridColumns ) 
				{
					if ( !dataGridColumnsProps[columnIndex].aincrement ) 
					{
						if ( dataGridColumnsProps[columnIndex].notnull )
							tableRow[ dgColumn.dataField ] = 'REQUIRED';
						else
							tableRow[ dgColumn.dataField ] = 'NULL';
					} 
					else 
					{
						tableRow[ dgColumn.dataField ] = 'NULL';
					}
					columnIndex++;
				}
				
				tableRow['GUID'] = UIDUtil.createUID();
				
				dataGridProvider.addItem( tableRow );
				
				dataTable.selectedIndex = dataGridProvider.length - 1;
				
				updateQueueLength();
			}
			
			protected function addRow_clickHandler( event : MouseEvent ) : void
			{
				addRowBtnClickHandler();
			}
			
			private function getXMLByRow( item : Object ) : XML
			{
				var requestXMLParam:XML = new XML( <data /> );
				var xmlRow:XML = new XML( <row id={item["id"]} /> );
				
				for each ( var dataGridCell:Object in dataGridColumns ) 
				{
					xmlRow.appendChild(
						<cell name={dataGridCell.dataField}>
						{item[dataGridCell.dataField]}
						</cell> );
				}
				
				requestXMLParam.appendChild( xmlRow );
				
				return requestXMLParam;
			}
			
			protected function commit_clickHandler( event : MouseEvent ) : void
			{
				var item : Object;
				var requestXMLParam:XML;
				
				for each ( item in dataGridProvider )
				{
					requestXMLParam = getXMLByRow( item );
					for  ( var tt : String in item )
					{
						
						if ( dataGridProviderOld[item["id"]] )
						{
							if ( dataGridProviderOld[item["id"]][tt] && item[tt] != dataGridProviderOld[item["id"]][tt] )
							{
								queue.addRequest(
									item['GUID'],
									'update_row',
									requestXMLParam.toXMLString() );
								
								break;
							}
						}
						else
						{
							queue.addRequest(
								item['GUID'],
								'add_row',
								requestXMLParam.toXMLString() );
							break;
						}
					}
				}
				
				if ( queue.length > 0 )
				{
					queue.addEventListener( QueueEvent.QUEUE_COMPLETE, queueCommitCompleteHandler );
					queue.execute();
				
					currentState = "Progress";
				
					updateQueueLength();
				}
			}
			
			private function queueCommitCompleteHandler( event : QueueEvent ) : void 
			{
				queue.removeEventListener( QueueEvent.QUEUE_COMPLETE, queueCommitCompleteHandler );
				
				updateTable();
			}
			
			public function updateTable() : void
			{
				updateQueueLength();
				
				/* ReRequest rows count */
				queue.reset();
				queue.addRequest( UIDUtil.createUID(), 'get_structure', '', structureResponseHandler, null);
				queue.addRequest( UIDUtil.createUID(), 'get_count', '', countResponseHandler, null );
				
				/* ReRequest page */
				queue.addRequest(
					UIDUtil.createUID(),
					'get_data',
					'<range><limit>' + AMOUNT.toString() + '</limit><offset>' + String(AMOUNT * currentPage) + '</offset></range>',
					refreshDataGrid, null);
				
				verticalScrollPositionStore = dataTable.verticalScrollPosition;
				
				queue.addEventListener( QueueEvent.QUEUE_COMPLETE, updateTableCompleteHandler );
				queue.execute();
				currentState = "Progress";
			}
			
			private function updateTableCompleteHandler( event : QueueEvent ) : void 
			{
				queue.removeEventListener( QueueEvent.QUEUE_COMPLETE, updateTableCompleteHandler );
				currentState = "Result";
			}
			
			private function refreshDataGrid( message : String ) : void 
			{
				getPageResponseHandler( message );
				dataTable.verticalScrollPosition = verticalScrollPositionStore;
			}
			
			protected function deleteRow_clickHandler( event : MouseEvent ) : void
			{				
				var selectedRowsArray : Array;
				
				try 
				{
					selectedRowsArray = dataTable.selectedItems;
					
					if ( !selectedRowsArray || selectedRowsArray.length == 0 )
						return;
				}
				catch ( err : Error ) 
				{
					return; 
				}
				
				for each ( var row : Object in selectedRowsArray )
				{
				
					var requestXMLParam:XML = new XML( <data><row id={row['id']} /></data> );
					
					queue.addRequest(
						row['GUID'].toString(),
						'delete_row',
						requestXMLParam.toXMLString() );
				
					dataGridProvider.removeItemAt( dataGridProvider.getItemIndex( row ) );
				
				}
				
				updateQueueLength();
			}
			
			protected function discard_clickHandler( event : MouseEvent ) : void
			{
				queue.reset();
				delete pages[currentPage];
				getPageRequest();
			}
			
			private function amountApplyBtnClickHandler() : void 
			{
				try 
				{
					AMOUNT = int(amount.text);
				}
				catch (err:Error) 
				{
					AMOUNT = 500;
				}
				
				AMOUNT = AMOUNT > 2000 ? 2000 : AMOUNT;
				AMOUNT = AMOUNT <= 0 ? 500 : AMOUNT;
				
				pages = [];
				currentPage = 0;
				getPageRequest();
			}
			
			protected function button1_clickHandler(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				try 
				{
					AMOUNT = int(amount.text);
				}
				catch (err:Error) 
				{
					AMOUNT = 500;
				}
				
				AMOUNT = AMOUNT > 2000 ? 2000 : AMOUNT;
				AMOUNT = AMOUNT <= 0 ? 500 : AMOUNT;
				
				pages = [];
				currentPage = 0;
				getPageRequest();
			}
			
			protected function dataTable_doubleClickHandler(event:MouseEvent):void
			{
				addRowBtnClickHandler();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:states>
		<s:State name="Progress"/>
		<s:State name="Result"/>
	</s:states>
	
	<s:VGroup width="100%" height="100%">
		
		<s:Group width="100%" height="100%" >
			
			<components1:VDOMAdvancedDataGrid id="dataTable" dataProvider="{dataGridProvider}"
											  width="100%" height="100%" horizontalScrollPolicy="auto" verticalScrollPolicy="auto"				 
											  draggableColumns="true" sortableColumns="true" doubleClickEnabled="true"
											  allowMultipleSelection="true" alpha.Result="1" alpha.Progress="0.4"
											  doubleClick="dataTable_doubleClickHandler(event)"/>
			
			<mx:Image source="@Embed('assets/spinner.swf')" width="100%" height="100%" verticalAlign="middle" horizontalAlign="center"  includeIn="Progress" scaleContent="false" maintainAspectRatio="true"/>
			
		</s:Group>
				<s:Group width="100%">
			<s:Rect width="100%" height="70">
				<s:fill>
					<s:SolidColor color="#F0F0F0"/>
				</s:fill>
			</s:Rect>
			
			<s:VGroup width="100%" height="102">
				
				<components1:SearchBox target="{dataTable}" height="22"/>
				
				<s:HGroup width="100%" height="20" horizontalAlign="left" verticalAlign="middle" gap="0" paddingRight="0" paddingLeft="0">
					<s:Label text="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_pages' ) }" />
					<mx:HBox id="pagesArea" width="100%" height="20" verticalAlign="middle" horizontalGap="0"
							 disabledOverlayAlpha="0.1" />
					<s:Label text="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_records' ) }" fontSize="9" />
					<s:TextInput id="amount" borderColor="#999999" fontWeight="bold" height="20" fontSize="9" width="45" />
					<s:Button height="20" label="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_apply' ) }" cornerRadius="0" click="button1_clickHandler(event)"/>
				</s:HGroup>
				
				<s:HGroup width="100%" height="50" verticalAlign="top" horizontalAlign="left" paddingLeft="10" paddingTop="5">
					
					<s:Button id="addRow" height="20" label="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_add_row' ) }" click="addRow_clickHandler(event)"/>
					
					<s:Button id="deleteRow" height="20" label="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_delete_row' ) }" click="deleteRow_clickHandler(event)"/>
					
					<s:Button id="commit" height="20" label="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_commit' ) }" click="commit_clickHandler(event)"/>
					
					<s:Button id="discard" height="20"  label="{ resourceManager.getString( 'DataBase_General', 'data_table_editor_discard' ) }" click="discard_clickHandler(event)"/>
					
				</s:HGroup>
			</s:VGroup>
			
			
		</s:Group>
			
	</s:VGroup>
	
</s:SkinnableContainer>

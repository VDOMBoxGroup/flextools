<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml">
	
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.events.TreeEvent;
			
			import net.vdombox.helpeditor.controller.events.PagesSyncronizationEvent;
			import net.vdombox.helpeditor.model.proxy.SQLProxy;
			import net.vdombox.helpeditor.view.itemrenderers.CheckBoxesTreeItemRenderer;
			
			public var syncGroupName : String;
			
			private var sqlProxy : SQLProxy = new SQLProxy();
			
			private var products : Object = sqlProxy.getAllProducts();
			
			private var lastSelectedPageName : String;
			
			public function get pages () : XMLList
			{
				var xmlList : XMLList = new XMLList();
				
				if (!products)
					return null;
				
				for each (var product : Object in products)
				{
					var xmlProductPages : XML = product.toc;
					
					xmlList += xmlProductPages;
				}
				
				return xmlList;
			}
			
			public function updatePages () : void
			{
				pagesTree.dataProvider = null;
				pagesTree.dataProvider = pages;
				
			}
			
			private function get lastSelectedPage () : XML
			{
				for each (var product : XML in pagesTree.dataProvider)
				{
					var pageName : String = product.@name;
					
					if (pageName == lastSelectedPageName)
						return product;
					
					for each (var page : XML in product..page)
					{
						pageName = page.@name;
						if (pageName == lastSelectedPageName)
							return page;
					}
				}
				
				return null;
			}
			
			private function treeItemRenderer_getCurrentSyncGroupNameHandler (event : PagesSyncronizationEvent) : void
			{
				var selectedTreeItemRenderer : CheckBoxesTreeItemRenderer = event.target as CheckBoxesTreeItemRenderer;
				
				selectedTreeItemRenderer.currentSyncGroupName = syncGroupName;
			}
			
			protected function pagesTree_initializeHandler(event:FlexEvent):void
			{
				pagesTree.addEventListener(PagesSyncronizationEvent.GET_CUR_SYNC_GROUP_NAME, treeItemRenderer_getCurrentSyncGroupNameHandler, true);
			}
			
			protected function pagesTree_changeHandler(event:ListEvent):void
			{
				if (!pagesTree.selectedItem)
					return;
				
				var selectedPage : XML = pagesTree.selectedItem as XML;
				lastSelectedPageName = selectedPage.@name;
			}
			
		]]>
	</mx:Script>
	
	<mx:Tree id="pagesTree"
			 width="100%" height="100%"
			 itemRenderer="net.vdombox.helpeditor.view.itemrenderers.CheckBoxesTreeItemRenderer"
			 labelField="@title"
			 defaultLeafIcon="{null}"
			 folderClosedIcon="{null}"
			 folderOpenIcon="{null}"
			 initialize="pagesTree_initializeHandler(event)"
			 change="pagesTree_changeHandler(event)"/>
	
</mx:VBox>

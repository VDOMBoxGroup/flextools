<?xml version="1.0" encoding="utf-8"?>
<mx:Application 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute" xmlns:controls="qs.controls.*" xmlns:local="*" xmlns:svg="com.zavoo.svg.*" 
	creationComplete="{onCreationComplete();}" width="100%" height="100%" >
	
	
	<mx:Style>
		ToolTip {
			fontFamily:"Tahoma";
		}
	</mx:Style>
	
	<mx:Script>
		<![CDATA[
			import mx.utils.UIDUtil;
			import mx.containers.Tile;
			import mx.controls.Alert;
			import mx.controls.Label;
			import mx.core.Container;
			import mx.binding.utils.BindingUtils;
			import qs.controls.flexBookClasses.FlexBookEvent;
			import com.zavoo.svg.SVGViewer;
			import mx.events.FlexEvent;
			
			private var dataSourceUrl:String;
			private var hostName:String;
			private var hasCover:Boolean = false;
			
			private function onCreationComplete():void
			{
				var flashVars:Object = Application.application.parameters;
				var pageRange:int = 0;
				var startPage:int = 0;
				
				try {
					this.setStyle('backgroundColor', flashVars.bgcolor);
				}
				catch (err:Error) {	}
				
				
				try {
					pageRange = flashVars.pagerange;
					startPage = flashVars.startpage;
					dataSourceUrl = flashVars.datasource;
					__fb.width = flashVars.width;
					__fb.height = flashVars.height;
					this.hasCover = flashVars.hascover == "1";
				}
				catch (err:Error) {
					Alert.show(err.message, "The following error occurred:");
					return;
				}
				
				if (__fb.width == 0 || __fb.height == 0) {
					__fb.width = 640;
					__fb.height = 480;
				}
				
				if (hasCover) {
					__fb.cover = new SVGPage();
					__fb.backCover = new SVGPage();
					SVGPage(__fb.cover).loadURL(dataSourceUrl + '?page=0&timestamp=' +  UIDUtil.createUID());
					SVGPage(__fb.backCover).loadURL(dataSourceUrl + '?page=' + String(pageRange + 1) + '&timestamp=' +  UIDUtil.createUID());
				}
				
				
				var pages:Array = [];
				var i: int = pageRange;
				while(i > 0) {
			    	pages.push(new SVGPage());
			    	i--;
			 	}
				__fb.content = pages;
			    BindingUtils.bindProperty(this, "pageChange", __fb, "currentPageIndex");
			    if (startPage >= 0 && startPage <= __fb.maximumPageIndex - 1)
					__fb.turnToPage(startPage, false);
			}
			
			public function set pageChange(index:*):void {
				if (index == -1 || index > __fb.maximumPageIndex - 1)
					return;
				
				var currentPage:* = __fb.content[index];
				if (SVGPage(currentPage).numChildren == 0)
					SVGPage(currentPage).loadURL(dataSourceUrl + '?page=' + String(index + 1) + '&timestamp=' +  UIDUtil.createUID());
				
				if (index >= 0 && index + 1 <= __fb.maximumPageIndex - 1) {
					if (SVGPage(__fb.content[index + 1]).numChildren == 0)
						SVGPage(__fb.content[index + 1]).loadURL(dataSourceUrl + '?page=' + String(index + 2) + '&timestamp=' +  UIDUtil.createUID());
				}
				
				if (index - 1 >= 0 && index <= __fb.maximumPageIndex - 1) {
					if (SVGPage(__fb.content[index - 1]).numChildren == 0)
						SVGPage(__fb.content[index - 1]).loadURL(dataSourceUrl + '?page=' + String(index) + '&timestamp=' +  UIDUtil.createUID());
				}
				
			}
			
			private function turnLeft():void {
				if (__fb.currentPageIndex > 0)
					__fb.turnToPage(__fb.currentPageIndex - 1);
			}
			
			private function turnRight():void {
				if (__fb.currentPageIndex + 1 < __fb.maximumPageIndex)
					__fb.turnToPage(__fb.currentPageIndex + 1);
			}
			
		]]>
	</mx:Script>
	
	<mx:VBox width="100%" height="100%" horizontalAlign="center" verticalAlign="middle"
		paddingBottom="5">
		
		<controls:FlexBook id="__fb" width="100%" height="100%" horizontalCenter="0" verticalCenter="0">
	<!--
			<controls:cover>
				<mx:Canvas backgroundColor="#000000" color="#FFFFFF" />
			</controls:cover>
	
			<controls:backCover>
				<mx:Canvas backgroundColor="#000000" color="#FFFFFF" />
			</controls:backCover>
	-->
		</controls:FlexBook>
		<mx:HBox width="50%" horizontalGap="2" horizontalAlign="center" height="20" verticalAlign="middle" >
			<mx:LinkButton width="25" click="{turnLeft();}" icon="@Embed('/images/previous.png')" />
			<mx:HRule width="30" />
			<mx:LinkButton width="25" click="{turnRight();}" icon="@Embed('/images/next.png')" />
		</mx:HBox>
	</mx:VBox>
		
</mx:Application>
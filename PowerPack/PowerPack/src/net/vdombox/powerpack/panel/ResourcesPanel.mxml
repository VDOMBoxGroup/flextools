<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
         xmlns:controls="net.vdombox.powerpack.lib.extendedapi.controls.*"
         paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5"
         width="100%" height="100%"
         label="Resources"
         initialize="initializeHandler(event)"
         show="{updateResourcesList();}">
	
	<mx:Script>
		<![CDATA[
        import mx.controls.advancedDataGridClasses.AdvancedDataGridItemRenderer;
        import mx.events.FlexEvent;
        import mx.events.ListEvent;
        import mx.managers.CursorManager;

        import net.vdombox.powerpack.customize.ImageTooltip;

        import net.vdombox.powerpack.events.ResourcesEvent;
        import net.vdombox.powerpack.lib.player.managers.ContextManager;
        import net.vdombox.powerpack.lib.player.popup.AlertPopup;
        import net.vdombox.powerpack.managers.CashManager;
        import net.vdombox.powerpack.managers.ProgressManager;

        import net.vdombox.powerpack.managers.ResourcesManager;
        import net.vdombox.powerpack.template.BuilderTemplate;

        public function refresh() : void
        {
            updateResourcesList();
            clearSearch();
        }

        private function updateResourcesList() : void
        {
            grid.dataProvider = resourcesManager.allResources;
            grid.invalidateList();
        }

        private function get resourcesManager() : ResourcesManager
        {
            return ResourcesManager.getInstance();
        }

        private function addResource() : void
        {
            CursorManager.setBusyCursor();

            ProgressManager.complete();
            ProgressManager.show( ProgressManager.DIALOG_MODE, false );

            resourcesManager.addEventListener( ResourcesEvent.COMPLETE, addResourceCompleteHandler );
            resourcesManager.addEventListener( ResourcesEvent.CANCEL, addResourceCompleteHandler );
            resourcesManager.addEventListener( ResourcesEvent.ERROR, addResourceCompleteHandler );

            resourcesManager.addResource();

            function addResourceCompleteHandler( event : ResourcesEvent ) : void
            {
                resourcesManager.removeEventListener( ResourcesEvent.COMPLETE, addResourceCompleteHandler );
                resourcesManager.removeEventListener( ResourcesEvent.CANCEL, addResourceCompleteHandler );
                resourcesManager.removeEventListener( ResourcesEvent.ERROR, addResourceCompleteHandler );

                CursorManager.removeBusyCursor();
                ProgressManager.complete();

                switch ( event.type )
                {
                    case ResourcesEvent.ERROR :
                    {
                        AlertPopup.show( event.errorMsg, "Error" );
                        break;
                    }
                }
            }
        }

        private function removeResource() : void
        {
            var resourceXML : XML = grid.selectedItem as XML;

            if ( !resourceXML )
            {
                return;
            }

            CursorManager.setBusyCursor();

            ProgressManager.complete();
            ProgressManager.show( ProgressManager.DIALOG_MODE, false );

            resourcesManager.removeResource( resourceXML.@ID );

            CursorManager.removeBusyCursor();
            ProgressManager.complete();
        }


        private function resourcesChangedHandler( event : ResourcesEvent ) : void
        {
            updateResourcesList();
        }

        private function initializeHandler( event : FlexEvent ) : void
        {
            resourcesManager.addEventListener( ResourcesEvent.CHANGED, resourcesChangedHandler );
        }

        private function previewImage( resourceXML : XML ) : void
        {
            var curTpl : BuilderTemplate = ContextManager.templates[0];
            var index : XML = CashManager.getIndex( curTpl.fullID );
            var thumbFile : File = CashManager.cashFolder.resolvePath( index.@folder ).resolvePath( resourceXML.@thumb );

            showImageTooltip( thumbFile );
        }

        private var assistTooltip : ImageTooltip = new ImageTooltip();

        public function showImageTooltip( imageFile : File ) : void
        {
            hideImageTooltip();

            var distance : int = 5;
            var point : Point = localToGlobal( new Point( mouseX, mouseY ) );

            var defX : Number = point.x + distance;
            var defY : Number = point.y + distance;

            if ( assistTooltip.isShowing )
            {
                assistTooltip.defaultX = defX;
                assistTooltip.defaultY = defY;

                assistTooltip.invalidateDisplayList();
                return;
            }

            assistTooltip.show( this, defX, defY, imageFile );
        }

        public function hideImageTooltip() : void
        {
            if ( !assistTooltip )
            {
                return;
            }

            if ( !assistTooltip.isShowing )
            {
                return;
            }

            assistTooltip.dispose();
        }

        private function grid_mouseOverHandler( event : MouseEvent ) : void
        {
            if ( event.target is AdvancedDataGridItemRenderer )
            {
                var resourceXML : XML = event.target.data as XML;

                if ( resourceXML.@category == "image" )
                {
                    previewImage( resourceXML );
                }

            }
        }

        private function grid_mouseOutHandler( event : MouseEvent ) : void
        {
            if ( event.target is AdvancedDataGridItemRenderer )
            {
                hideImageTooltip();
            }
        }

        private function nameFilter() : void
        {
            if (!grid.dataProvider)
                return;

            grid.dataProvider.filterFunction = nameSearch;
            grid.dataProvider.refresh();
        }

        private function nameSearch( item : Object ) : Boolean
        {
            var isMatch : Boolean = false;

            if ( item.@name.toString().toLowerCase().search( searchTextBox.text.toString().toLowerCase() ) != -1 )
            {
                isMatch = true;
            }

            return isMatch;
        }

        private function idFilter() : void
        {
            if (!grid.dataProvider)
                return;

            grid.dataProvider.filterFunction = idSearch;
            grid.dataProvider.refresh();
        }

        private function idSearch( item : Object ) : Boolean
        {
            var isMatch : Boolean = false;

            if ( item.@ID.toString().toLowerCase().search( searchTextBox.text.toString().toLowerCase() ) != -1 )
            {
                isMatch = true;
            }

            return isMatch;
        }

        private function filterValues() : void
        {
            if (searchByComboBox.selectedLabel == "ID")
                idFilter();
            else if (searchByComboBox.selectedLabel == "Name")
                nameFilter();
        }

        private function clearSearch() : void
        {
            searchTextBox.text='';
            filterValues();
        }


        ]]>
	</mx:Script>
	
	<mx:HBox horizontalAlign="right" width="100%" verticalAlign="middle">
        <mx:Label text="Search by:"/>
        <mx:ComboBox id="searchByComboBox" change="filterValues()">
            <mx:dataProvider>
                <mx:String>Name</mx:String>
                <mx:String>ID</mx:String>
            </mx:dataProvider>
        </mx:ComboBox>
        <mx:TextInput id="searchTextBox" width="100" change="filterValues()"/>
        <mx:Image source="{ContextManager.deleteClass}" click="clearSearch()"/>

        <mx:Spacer width="100%"/>
        <mx:Button label="Add" click="addResource();"/>
		<mx:Button label="Remove" click="removeResource();" enabled="{grid.selectedIndex >= 0}"/>
	</mx:HBox>
	
	<controls:SuperDataGrid id="grid"
                            designViewDataType="flat"
							width="100%" height="100%"
							defaultLeafIcon="{null}"
							folderOpenIcon="{null}"
							folderClosedIcon="{null}"
                            dataProvider="{resourcesManager.allResources}"
                            mouseOver="grid_mouseOverHandler(event)"
                            mouseOut="grid_mouseOutHandler(event)"
                            dragEnabled="true">
		
		<controls:columns>
			<mx:AdvancedDataGridColumn
				headerText="ID"
				dataField="@ID"
				dataTipField="@ID"
				showDataTips="true"/>

            <mx:AdvancedDataGridColumn
                    headerText="Name"
                    dataField="@name"
                    dataTipField="@name"
                    showDataTips="true" />

            <mx:AdvancedDataGridColumn
                    headerText="Type"
                    dataField="@type"
                    dataTipField="@type"
                    showDataTips="true" />

		</controls:columns>

	</controls:SuperDataGrid>

</mx:VBox>

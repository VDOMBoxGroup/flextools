<?xml version="1.0" encoding="utf-8"?>
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:controls="ExtendedAPI.com.controls.*" 
	width="100%" height="100%"
	verticalGap="0" 
	label="{LanguageManager.sentences.problems_title}"
	initialize="init()"
	icon="{ContextManager.problemsClass}">
	
	<mx:String id="description">0 errors, 0 warnings</mx:String>
	<mx:Array id="array"/>
	
	<mx:Script>
		<![CDATA[
		import net.vdombox.powerpack.com.managers.ContextManager;
		import net.vdombox.powerpack.com.BasicError;

		import mx.utils.StringUtil;
		import mx.collections.HierarchicalData;
		import mx.binding.utils.BindingUtils;

		import net.vdombox.powerpack.com.managers.LanguageManager;

		static private var defaultCaptions : Object = {
			problems_title: "Problems",
			description: "Description",
			problems_overall: "{0} errors, {1} warnings",
			problems_errors: "Errors ({0} item(s))",
			problems_warnings: "Warnings ({0} item(s))"
		}

		static private var _classConstructed : Boolean = classConstruct();

		static private function classConstruct() : Boolean {
			LanguageManager.setSentences( defaultCaptions );
			return true
		}

		private function init() : void {
			BindingUtils.bindSetter( populateProblems, this, "array" );
		}

		private function populateProblems( array : Array ) : void {
			grid.dataProvider = null;
			description = StringUtil.substitute( LanguageManager.sentences['problems_overall'], 0, 0 );

			if ( array && array.length > 0 ) {
				var errors : Array = [];
				var warnings : Array = [];

				for each( var item : Object in array ) {
					if ( item.error.severity == BasicError.FATAL )
						errors.push( { Description: item.error.message, data:item, icon:ContextManager.errClass } );
					else
						warnings.push( { Description: item.error.message, data:item,  icon:ContextManager.warnClass } );
				}

				description = StringUtil.substitute( LanguageManager.sentences['problems_overall'],
						errors.length, warnings.length );

				var problems : Array = [];

				if ( errors.length > 0 )
					problems.push( {	Description:	 StringUtil.substitute( LanguageManager.sentences['problems_errors'],
									errors.length ),
								children:errors } );

				if ( warnings.length > 0 )
					problems.push( {	Description:	StringUtil.substitute( LanguageManager.sentences['problems_warnings'],
									warnings.length ),
								children:warnings } );

				grid.dataProvider = new HierarchicalData( problems );
			}
		}

		private function onDoubleClick( event : MouseEvent ) : void {
			//
		}
		]]>
	</mx:Script>


	<mx:ControlBar visible="false" includeInLayout="false"></mx:ControlBar>
	
	<mx:Label text="{description}"/>	
									
	<controls:SuperDataGrid doubleClickEnabled="true"  
		id="grid" 
		designViewDataType="tree" 
		width="100%" height="100%"
		defaultLeafIcon="{null}"
		folderOpenIcon="{ContextManager.expandClass}"
		folderClosedIcon="{ContextManager.expandClass}"
		doubleClick="onDoubleClick(event)">
	  	<controls:columns>
	    	<mx:AdvancedDataGridColumn	
	    		headerText="{LanguageManager.sentences.description}"    			    		
	    		dataTipField="Description"
	    		dataField="Description"
	    		editable="false"
	    		showDataTips="true"/>
	  	</controls:columns>
	</controls:SuperDataGrid>	

</mx:VBox>

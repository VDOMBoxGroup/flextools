<?xml version="1.0" encoding="utf-8"?>
<ModalDialog
	xmlns="net.vdombox.powerpack.dialog.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	width="500" height="300"
	title="Build installer"
	creationComplete="onShow()"
	xmlns:controls="net.vdombox.powerpack.lib.extendedapi.controls.*" 
	xmlns:customize="net.vdombox.powerpack.customize.*"
	resizable="false">
	
	<customize:TemplateDefaultLogo id="defaultLogo" />
	
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.utils.StringUtil;
			
			import net.vdombox.powerpack.Template;
			import net.vdombox.powerpack.lib.extendedapi.containers.SuperAlert;
			import net.vdombox.powerpack.lib.extendedapi.utils.FileUtils;
			import net.vdombox.powerpack.managers.ContextManager;
			import net.vdombox.powerpack.managers.LanguageManager;
			import net.vdombox.powerpack.managers.ProgressManager;
			import net.vdombox.powerpack.sdkcompiler.SDKCompiler;
			import net.vdombox.powerpack.sdkcompiler.SDKCompilerEvent;
			
			private static var _dialog	: AirBuildProperties;
			
			public var templateStructure : XML;
			
			private var installerTplXml : XML = 
				 <application xmlns="http://ns.adobe.com/air/application/2.0">
				  <version>1.2.0.7613</version> 
				 <initialWindow>
				  <content>Installer.swf</content> 
				  <systemChrome>none</systemChrome> 
				  <transparent>true</transparent> 
				  <maximizable>false</maximizable> 
				  <resizable>false</resizable> 
				  </initialWindow>
				  <installFolder>VDOM</installFolder> 
				  <programMenuFolder>VDOM</programMenuFolder> 
				 <icon>
				  <image16x16>assets/icons/Installer16.png</image16x16> 
				  <image32x32>assets/icons/Installer32.png</image32x32> 
				  <image48x48>assets/icons/Installer48.png</image48x48> 
				  <image128x128>assets/icons/Installer128.png</image128x128> 
				  </icon>
				  </application>;
			
			private static const DEFAULT_OUTPUT_FOLDER		: String = File.documentsDirectory.nativePath;
			private static const DEFAULT_OUTPUT_FILE_NAME	: String = "appInstaller";
			private static const DEFAULT_APP_PATH			: String = "";
			
			private var shObject : SharedObject = SharedObject.getLocal( "air_properties" );
			private  function onShow():void
			{
				installerFolder.text = sharedOutputFolder || DEFAULT_OUTPUT_FOLDER;
				
				installerFileName.text = sharedOutputFileName || DEFAULT_OUTPUT_FILE_NAME;
				
				installerApplication.text = sharedApp || DEFAULT_APP_PATH;
			}
			
			private function get sharedOutputFolder () : String
			{
				return shObject.data.hasOwnProperty("outputInstallerFolder") ? shObject.data["outputInstallerFolder"] : "";
			}
			
			private function get sharedOutputFileName () : String
			{
				return shObject.data.hasOwnProperty("outputInstallerFileName") ? shObject.data["outputInstallerFileName"] : "";
			}
			
			private function get sharedApp () : String
			{
				return shObject.data.hasOwnProperty("installerApplication") ? shObject.data["installerApplication"] : "";
			}
			
			private function get outputFolderPath () : String
			{
				return StringUtil.trim(installerFolder.text) || DEFAULT_OUTPUT_FOLDER;
			}
			
			private function get outputFileName () : String
			{
				return StringUtil.trim(installerFileName.text) || DEFAULT_OUTPUT_FILE_NAME;
			}
			
			private function get appPath () : String
			{
				return StringUtil.trim(installerApplication.text) || DEFAULT_APP_PATH;
			}
			
			static public function show( templateXmlStructure : XML, closeHandler : Function = null ) : AirBuildProperties
			{
				
				
				if ( !_dialog || _dialog.closed )
				{
					_dialog = new AirBuildProperties();
					
					_dialog.templateStructure = templateXmlStructure;
					
					_dialog.open();
					_dialog.show( OK | CANCEL, closeHandler, null, OK );
				}
				else
				{
					_dialog.activate();
				}
				
				return _dialog;
			}
			
			protected override function removeDialog( buttonPressed : String ) : void
			{
				switch ( buttonPressed )
				{
					
					case BUTTON_NAME_OK:
					{
							_dialog.closeDetail = ModalDialog.OK;
							_dialog.close();
							
							shObject.data["outputInstallerFolder"] = outputFolderPath;
							shObject.data["outputInstallerFileName"] = outputFileName;
							shObject.data["installerApplication"] = appPath;
							
							shObject.flush();
						
						break;
					}
					case BUTTON_NAME_CANCEL:
					{
						_dialog.closeDetail = ModalDialog.CANCEL;
						_dialog.close();
						break;
					}
					default:
					{
						_dialog.closeDetail = ModalDialog.CANCEL;
						_dialog.close();
						break;
					}
				}
			}
			
			protected function btnOutputFolderClickHandler(event:MouseEvent):void
			{
				var file : File = new File(outputFolderPath);
				
				file.addEventListener(Event.SELECT, folderSelectHandler);
				
				file.browseForDirectory("Select output folder");
				
				
				function folderSelectHandler (evt:Event) : void
				{
					file.removeEventListener(Event.SELECT, folderSelectHandler);
					
					installerFolder.text = file.nativePath; 
				}
			}
			
			protected function btnBrowseApplicationClickHandler(event:MouseEvent):void
			{
				var fileFilter : FileFilter = new FileFilter("XML", "*.xml");
				
				var filePath : String = appPath || null;
				
				try
				{
					var file : File = new File(filePath);
					
					file.addEventListener(Event.SELECT, appSelectHandler);
					
					file.browseForOpen("Select application", [fileFilter]);
				}
				catch (e:Error)
				{
					file = new File();
					
					if (!file.hasEventListener(Event.SELECT))
						file.addEventListener(Event.SELECT, appSelectHandler);
					
					file.browseForOpen("Select application", [fileFilter]);
					
				}
				
				function appSelectHandler (evt:Event) : void
				{
					file.removeEventListener(Event.SELECT, appSelectHandler);
					
					if (isSselectedAppCorrect())
						installerApplication.text = file.nativePath;
					else
						installerApplication.text = DEFAULT_APP_PATH;
				}
				
				function isSselectedAppCorrect() : Boolean
				{
					var fileStream : FileStream = new FileStream();
					var appXml : XML;
					
					try 
					{
						fileStream.open(file, FileMode.READ);
						appXml = new XML( fileStream.readUTFBytes( fileStream.bytesAvailable ) );
						fileStream.close();
						
						var appId : String = appXml.Information.ID[0];
						
						if (!appId)
							return false;
					}
					catch (e:Error)
					{
						fileStream.close();
						return false;
					}
					
					return true;
				}
				
			}
			
			public function get currentTemplate() : Template
			{
				if ( !ContextManager.templates || ContextManager.templates.length == 0 )
					return null;
				
				return ContextManager.templates.getItemAt( 0 ) as Template;
			}
			
			private function saveTemplate () : void
			{ 
				if (!currentTemplate)
				{
					SuperAlert.show("No opened template", "Error");
					return;
				}
				
				currentTemplate.addEventListener( Event.COMPLETE, templateSaveCompleteHandler );
				currentTemplate.addEventListener( ErrorEvent.ERROR, templateSaveErrorHandler );
				
				currentTemplate.xmlStructure = templateStructure;
				currentTemplate.save();
				
				function templateSaveCompleteHandler(evt:Event) : void
				{
					currentTemplate.removeEventListener( Event.COMPLETE, templateSaveCompleteHandler );
					
					try
					{
						var tplTargetFile : File = File.applicationStorageDirectory.resolvePath("assets/template.xml");
						currentTemplate.file.copyTo(tplTargetFile, true);
					}
					catch (e:Error)
					{
						SuperAlert.show("Can't save template", "Error");
						return;
					}
					
					compilePlayer();
				}
				
				function templateSaveErrorHandler(evt:Event) : void
				{
					currentTemplate.removeEventListener( ErrorEvent.ERROR, templateSaveErrorHandler );
					
					SuperAlert.show("Can't save template", "Error");
				}
			}
			
			private function compilePlayer():void
			{
				var installerPropertiesXmlPrepeared : Boolean = prepareInstallerPropertiesXml();
				var installerAppPrepeared : Boolean = prepareInstallerApp();
				
				if (!installerPropertiesXmlPrepeared)
				{
					SuperAlert.show("Can't prepare installer properties xml", "Error");
					return;
				}
				
				var sdkCompiler : SDKCompiler = new SDKCompiler();
				
				if (!sdkCompiler.hasEventListener(SDKCompilerEvent.SDK_COMPILER_COMPETE))
					sdkCompiler.addEventListener(SDKCompilerEvent.SDK_COMPILER_COMPETE, sdkCompilerEventHandler); 
				
				if (!sdkCompiler.hasEventListener(SDKCompilerEvent.SDK_COMPILER_ERROR))
					sdkCompiler.addEventListener(SDKCompilerEvent.SDK_COMPILER_ERROR, sdkCompilerEventHandler);
				
				ProgressManager.complete();
				ProgressManager.source = null;
				ProgressManager.start( ProgressManager.DIALOG_MODE, false );
				
				sdkCompiler.buildInstallerPackage (outputFolderPath, outputFileName, installerAppPrepeared);
				
				function sdkCompilerEventHandler (evt:SDKCompilerEvent) : void
				{
					sdkCompiler.removeEventListener(SDKCompilerEvent.SDK_COMPILER_COMPETE, sdkCompilerEventHandler);
					sdkCompiler.removeEventListener(SDKCompilerEvent.SDK_COMPILER_ERROR, sdkCompilerEventHandler);
					
					ProgressManager.complete();
					
					if (evt.message)
					{
						SuperAlert.show(evt.message);
					}
				}
			}
			
			private function prepareInstallerApp() : Boolean
			{
				if (!appPath)
					return false;
				
				try
				{
					var sourceAppFile : File = new File(appPath);
					
					if (!sourceAppFile || !sourceAppFile.exists)
						return false;
					
					var targetAppFile : File = File.applicationStorageDirectory.resolvePath("app.xml");
					
					sourceAppFile.copyTo(targetAppFile, true);
				}
				catch (e:Error)
				{
					return false;
				}
				
				return true;
				
			}
			
			private function prepareInstallerPropertiesXml() : Boolean
			{
				var fileStream : FileStream = new FileStream();
				
				var targetXmlFile : File = File.applicationStorageDirectory.resolvePath("Installer-app.xml");
				
				var installerXML : XML;
				var targetXmlSource : String;
				
				try 
				{
					installerXML = installerTplXml;
					
					installerXML.id = "net.vdom." + currentTemplate.installerId;
					installerXML.name = currentTemplate.name;
					installerXML.filename = currentTemplate.name;
					
					installerXML.normalize();
					
					fileStream.open(targetXmlFile, FileMode.WRITE);
					fileStream.writeUTFBytes( '<?xml version="1.0" encoding="UTF-8"?>' + "\n" + installerXML.toXMLString() );
					fileStream.close();
				} 
				catch (e:Error)
				{
					fileStream.close();
					
					return false;
				}
				
				return true;
			}
			
			protected function btnBuildClickHandler(event:MouseEvent):void
			{
				saveTemplate();
			}
			
		]]>
	</mx:Script>
	
	<mx:VBox verticalGap="10"
			 paddingLeft="20" paddingRight="20" paddingTop="20" paddingBottom="20">
		<mx:VBox width="450" horizontalGap="3">
			<mx:Label text="Build installer" styleName="headerLabel" />
			<mx:Label text="Build and export VDOM Application Installer" />
			<mx:HRule width="100%" />
			<mx:Spacer height="20"/>
		</mx:VBox>
			 
		<mx:HBox width="450" horizontalGap="3">
			<mx:Label text="Output folder: " width="110"/>
			<mx:TextInput id="installerFolder" width="100%" editable="false" toolTip="{installerFolder.text}"/>
			<mx:Button label="Browse" click="btnOutputFolderClickHandler(event)" width="70"/>
		</mx:HBox>
		
		<mx:HBox width="450" horizontalGap="3">
			<mx:Label text="Output file name: " width="110"/>
			<mx:TextInput id="installerFileName" width="100%" restrict="a-zA-z0-9 \-_.()" toolTip="{installerFileName.text}"/>
			<mx:Spacer width="70"/>
		</mx:HBox>
		
		<mx:HBox width="450" horizontalGap="3">
			<mx:Label text="Application: " width="110"/>
			<mx:TextInput id="installerApplication" width="100%" editable="true" toolTip="{installerApplication.text}"/>
			<mx:Button label="Browse" click="btnBrowseApplicationClickHandler(event)" width="70"/>
		</mx:HBox>
			 
		<mx:Button label="Build" click="btnBuildClickHandler(event)" />
	</mx:VBox>
	
	<mx:ControlBar width="100%" height="100%" styleName="dlgFooter"/>
	
	
</ModalDialog>

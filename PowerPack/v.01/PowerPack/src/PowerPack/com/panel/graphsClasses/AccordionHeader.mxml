<?xml version="1.0" encoding="utf-8"?>
<CanvasButtonAccordionHeader
	xmlns="flexlib.containers.accordionClasses.*" 
	xmlns:flexlib="http://code.google.com/p/flexlib/" 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	verticalScrollPolicy="off" 
	horizontalScrollPolicy="off"
	doubleClickEnabled="true" 
	mouseChildren="true"
	dragEnter="dragEnterHandler(event);"
	dragDrop="dragDropHandler(event);"
	creationComplete="show();">
	
	<mx:Script>
		<![CDATA[
			import mx.core.FlexTextField;
			import mx.managers.PopUpManager;
			import mx.events.FlexEvent;
			import mx.controls.TextInput;
			import mx.binding.utils.BindingUtils;
			import mx.core.UITextField;
			import mx.core.IUITextField;
			import ExtendedAPI.com.containers.SuperAlert;
			import PowerPack.com.managers.LanguageManager;
			import PowerPack.com.panel.Graphs;
			import PowerPack.com.graph.GraphCanvas;
			import mx.controls.Alert;
			import mx.utils.ArrayUtil;
			import mx.containers.Accordion;
			import mx.core.IUIComponent;
			import mx.managers.DragManager;
			import mx.events.DragEvent;
			
			//----------------------------------
		    //  statusTextField
		    //----------------------------------
		
			/**
		     *  A reference to the UITextField that displays the status bar's text.
		     */
		    private var labelTextField:IUITextField;
		    public var txtInput:TextInput;	
		    
		    private var editing:Boolean;		
			
			[Bindable]
			private var _label:String;
			
			override public function set label(value:String):void 
			{
				super.label = "";
				
				_label = value;
			}
			
			private function show():void
			{
			}
			
			private function doubleClickHandler(event:Event):void
			{
				beginEdit();
			}
			
			private function closeTabHandler(event:Event):void
			{
				var accordChild:AccordionChild = data as AccordionChild;
				
				if(accordChild.graphs && accordChild.graphs.length>0)
				{	
					SuperAlert.show(LanguageManager.sentences['msg_cannot_remove_nonempty_cat']);					
					return;
				}
					
				event.stopPropagation();
				accordChild.parent.removeChild(accordChild);
			}
			
			public function beginEdit():void
			{
				if(editing)
					return;
				
				editing = true;
				
				if(labelTextField.parent)
					boxHeader.removeChild(DisplayObject(labelTextField));
					
				boxHeader.addChildAt(txtInput, 0);
				txtInput.visible = true;
				txtInput.setFocus();				

				invalidateDisplayList();
			}
			
			public function endEdit(event:Event):void
			{				
				if(!editing)
					return;
				
				var bCommit:Boolean = true;
				var bEndEdit:Boolean = true;
				
				if(event is KeyboardEvent)
				{
					// escape
				    if(KeyboardEvent(event).charCode == 27)
				    {
		    			bCommit = false;
						bEndEdit = true;
		    		}
		    		// enter
				    else if(KeyboardEvent(event).charCode == 13)
				    {
		    			bCommit = true;
						bEndEdit = true;
				    }
					else
						bEndEdit = false;
		  		}
				
				if(bEndEdit)
				{		   
					editing = false;
					 	
					if(bCommit)
						data.label = txtInput.text;
					else
						txtInput.text = _label;
					
					txtInput.visible = false;
					
					txtInput.validateNow();

					if(txtInput.parent)
						boxHeader.removeChild(txtInput);
					
					boxHeader.addChildAt(DisplayObject(labelTextField), 0);
					
					invalidateDisplayList();
					
					if(bCommit)
						EventDispatcher(data).dispatchEvent(new Event("captionChanged"));
				}
			}
			
		    private function dragEnterHandler(event:DragEvent):void
		    {
	            if (	event.dragSource.hasFormat("items") && 
			            event.dragSource.dataForFormat("items") &&
	            		(event.dragSource.dataForFormat("items") as Array).length>0)
	            {
	            	if(	(event.dragSource.dataForFormat("items") as Array)[0] is GraphCanvas &&
	            		event.target.data.lstGraphs != event.dragInitiator)
	            	{
	                	DragManager.acceptDragDrop(IUIComponent(event.target));
		                DragManager.showFeedback(DragManager.MOVE);
		            }
	            }	    	
		    }
		    
		    private function dragDropHandler(event:DragEvent):void
		    {
		    	var items:Array = 
	                    event.dragSource.dataForFormat("items") as Array;
		    	
		    	if(items[0] is GraphCanvas)
		    	{
		    		var graph:GraphCanvas = items[0] as GraphCanvas;
		    		var source:AccordionChild = AccordionChild(event.dragInitiator.parent);
		    		var destination:AccordionChild = AccordionChild(event.target.data);
					
					source.graphs.removeItemAt(source.graphs.getItemIndex(graph));
					graph.category = destination.label;					
					destination.graphs.addItem(graph);
					
					Graphs(destination.parent.parent.parent.parent).selectGraph(graph);
		    	}
		    }
		    
			override protected function createChildren():void
			{
       			super.createChildren();
       			
       			if (!labelTextField)
			    {
			        labelTextField = IUITextField(createInFontContext(UITextField));
			        labelTextField.text = _label;
			        labelTextField.styleName = getStyle("labelTextStyleName");
			        labelTextField.enabled = true;
			        labelTextField.doubleClickEnabled = true;
			        labelTextField.addEventListener(MouseEvent.DOUBLE_CLICK, doubleClickHandler);
			        
			        boxHeader.addChildAt(DisplayObject(labelTextField), 0);
		    	}
		    	if(!txtInput)
		    	{
		    		txtInput = new TextInput();
		    		txtInput.text = _label;
		    		txtInput.addEventListener(FocusEvent.FOCUS_OUT, endEdit);
		    		txtInput.addEventListener(FlexEvent.ENTER, endEdit);
		    		txtInput.addEventListener(KeyboardEvent.KEY_DOWN, endEdit);
		    		txtInput.addEventListener(FocusEvent.FOCUS_IN, onFocus);
		    		
		    		function onFocus(event:Event):void {
		    			event.stopPropagation();
		    		}
		    	}
   			}
   			
   			override protected function measure():void
			{
				super.measure();
				
				measuredWidth = parent.width;
   			}

		    /**
		     *  @private
		     */
			override protected function updateDisplayList(unscaledWidth:Number,
														  unscaledHeight:Number):void
			{
				unscaledWidth = parent.width;

				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				width = unscaledWidth;	
				
				boxHeader.width = width;
				boxHeader.height = height;
				
				cbarHeader.x = boxHeader.width - cbarHeader.width;
				
				
				if(labelTextField.parent)
					lblFieldResize();
				
				if(txtInput.parent)
					edtFieldResize();				

				boxHeader.validateNow();
			}
			   
   			private function getTxtFieldWidth():int
   			{
   				var width:int = boxHeader.getExplicitOrMeasuredWidth() - cbarHeader.getExplicitOrMeasuredWidth()-
   					boxHeader.getStyle('paddingLeft') - boxHeader.getStyle('paddingRight');
   				
   				return width;
   			}
   			
   			private function lblFieldResize():void
   			{
   				var lblWidth:int = getTxtFieldWidth();
   				
   				labelTextField.text = _label;
   				labelTextField.x = 2;
   				labelTextField.y = (boxHeader.getExplicitOrMeasuredHeight() - labelTextField.textHeight) / 2-2;
   				labelTextField.width = lblWidth-labelTextField.x;
   				
   				if(labelTextField.x+labelTextField.textWidth>lblWidth)
   					labelTextField.truncateToFit("...");
   					
   				labelTextField.validateNow();   				  					
   			}
   			
   			private function edtFieldResize():void
   			{
   				var edtWidth:int = getTxtFieldWidth();
   				
   				txtInput.width = edtWidth;
   			}   			
	    			
		]]>
	</mx:Script>
	
	<mx:Canvas id="boxHeader">

		<mx:ControlBar id="cbarHeader" horizontalAlign="right" paddingTop="2" paddingBottom="2" paddingRight="2" paddingLeft="2">
		
			<flexlib:CanvasButton 
				paddingTop="0"
				paddingLeft="0"
				paddingBottom="0"
				paddingRight="0"
				cornerRadius="2"
				click="closeTabHandler(event);">
				<mx:Label text="x"/>
			</flexlib:CanvasButton>
		
		</mx:ControlBar>
		
	</mx:Canvas>
		
</CanvasButtonAccordionHeader>

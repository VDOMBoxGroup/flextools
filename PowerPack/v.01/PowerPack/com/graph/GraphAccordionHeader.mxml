<?xml version="1.0" encoding="utf-8"?>
<CanvasButtonAccordionHeader verticalScrollPolicy="off" horizontalScrollPolicy="off"
	xmlns="flexlib.containers.accordionClasses.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	dragEnter="dragEnterHandler(event);"
	dragDrop="dragDropHandler(event);"
	doubleClickEnabled="true" xmlns:code="http://code.google.com/p/flexlib/" 
	creationComplete="show();">
	
	<mx:Script>
		<![CDATA[
			import mx.utils.ArrayUtil;
			import mx.containers.Accordion;
			import mx.core.IUIComponent;
			import mx.managers.DragManager;
			import mx.events.DragEvent;
			
			[Bindable]
			private var _label:String;
			
			override public function set label(value:String):void 
			{
				super.label = "";
				
				_label = value;
			}
			
			private function show():void
			{
				lblCaption.percentWidth = 100;
			}
			
			private function doubleClickHandler(event:Event):void
			{
				beginEdit();
			}
			
			private function closeHandler(event:Event):void
			{
				//event.stopPropagation();
				EventDispatcher(data).dispatchEvent(new Event("closing"));
			}
			
			public function beginEdit():void
			{
				lblCaption.visible = false;
				lblCaption.width = 0;
				
				txtCaption.visible = true;
				txtCaption.percentWidth = 100;
				
				txtCaption.setFocus();				
			}
			
			public function endEdit(event:Event):void
			{				
				var bCommit:Boolean = true;
				var bEndEdit:Boolean = true;
				
				if(event is KeyboardEvent)
				{
					// escape
				    if(KeyboardEvent(event).charCode == 27)
				    {
		    			bCommit = false;
						bEndEdit = true;
		    		}
		    		// enter
				    else if(KeyboardEvent(event).charCode == 13)
				    {
		    			bCommit = true;
						bEndEdit = true;
				    }
					else
						bEndEdit = false;
		  		}
				
				if(bEndEdit)
				{		    	
					if(bCommit)
						data.label = txtCaption.text;
					else
						txtCaption.text = _label;
						
					txtCaption.visible = false;
					txtCaption.width = 0;
				
					lblCaption.visible = true;
					lblCaption.percentWidth = 100;
					
					if(bCommit)
						EventDispatcher(data).dispatchEvent(new Event("captionChanged"));
				}
			}
			
		    private function dragEnterHandler(event:DragEvent):void
		    {
	            if (	event.dragSource.hasFormat("items") && 
			            event.dragSource.dataForFormat("items") &&
	            		(event.dragSource.dataForFormat("items") as Array).length>0 &&
	            		(event.dragSource.dataForFormat("items") as Array)[0] is GraphCanvas &&
	            		event.target.data.lstGraphs != event.dragInitiator)
	            {
	                DragManager.acceptDragDrop(IUIComponent(event.target));
	                DragManager.showFeedback(DragManager.MOVE);
	            }	    	
		    }
		    
		    private function dragDropHandler(event:DragEvent):void
		    {
		    	var items:Array = 
	                    event.dragSource.dataForFormat("items") as Array;
		    	
		    	var graph:GraphCanvas = items[0] as GraphCanvas;
		    	var source:GraphAccordionChild = GraphAccordionChild(event.dragInitiator.parent);
		    	var destination:GraphAccordionChild = GraphAccordionChild(event.target.data);
				
				source.graphs.removeItemAt(ArrayUtil.getItemIndex(graph, source.graphs.source));
				graph.category = destination.label;
				
				destination.graphs.addItem(graph);	
				
				GraphPanel(destination.parent.parent.parent.parent).curAccordionChild = destination;
				
				GraphPanel(destination.parent.parent.parent.parent).vsViewStack.selectedChild = graph;				
				Accordion(destination.parent).selectedChild = destination;
				destination.lstGraphs.selectedItem = graph;		
		    }
	    			
		]]>
	</mx:Script>
	
	<mx:HBox id="boxHeader" width="100%" verticalAlign="middle" horizontalGap="0">
		<mx:Label id="lblCaption" text="{_label}" paddingLeft="5" 
			doubleClickEnabled="true" doubleClick="doubleClickHandler(event);"/>
		<mx:TextInput id="txtCaption" text="{_label}" width="0" visible="false"
			focusOut="endEdit(event);" enter="endEdit(event);" keyDown="endEdit(event);" focusIn="event.stopPropagation();"/>
		<mx:ControlBar id="cbarHeader" horizontalAlign="right" paddingTop="2" paddingBottom="2" paddingRight="2" paddingLeft="2">
		<code:CanvasButton
			click="closeHandler(event);">
			<mx:Label text="x"/>
		</code:CanvasButton>
		</mx:ControlBar>
	</mx:HBox>
		
</CanvasButtonAccordionHeader>
